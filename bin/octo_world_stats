#!/usr/bin/perl

=head1 NAME

octo_world_stats - Octopussy World Statistics program

=head1 DESCRIPTION

octo_world_stats sends statistics data to octopussy.pm to see Octopussy usage in the world

=cut

use strict;
use warnings;

use Getopt::Long;
use POSIX qw(strftime);
use URI::Escape;

use AAT::Download;
use AAT::Syslog;
use Octopussy;
use Octopussy::App;
use Octopussy::Cache;
use Octopussy::Device;
use Octopussy::FS;
use Octopussy::Stats;
use Octopussy::World_Stats;

my $APPLI      = 'Octopussy';
my $PROG_NAME  = 'octo_world_stats';
my $UPDATE_URL = Octopussy::Parameter('url_world_stats_update');

my $quiet;

#
# MAIN
#
exit if (!Octopussy::App::Valid_User($PROG_NAME));

my $status = GetOptions('q|quiet' => \$quiet,);

my $conf = Octopussy::World_Stats::Configuration();
if ((defined $conf) && ($conf->{status} eq 'enabled'))
{
    my ($id, $country) = ($conf->{id}, $conf->{country});

    my $dir_pid = Octopussy::FS::Directory('running');
    my $cache   = Octopussy::Cache::Init('octo_dispatcher');
    my $version = Octopussy::Version();
    my $cpu     = Octopussy::Stats::CPU_Info();
    $cpu =~ s/\s+/ /g;
    $cpu = uri_escape($cpu);
    my $mem         = Octopussy::Stats::Mem_Total();
    my @devices     = Octopussy::Device::List();
    my @services    = Octopussy::Device::Services(@devices);
    my $nb_devices  = scalar @devices;
    my $nb_services = scalar @services;
    my @keys        = $cache->get_keys();
    my $timestamp   = strftime("%Y%m%d%H", localtime);

    foreach my $k (sort @keys)
    {
        if (   ($k =~ /^dispatcher_stats_hourly_(\d{10})$/)
            && ($1 !~ /^$timestamp$/))
        {
            my $hour = $1;
            my $url =
                  "$UPDATE_URL?id=$id&country=$country&version=$version"
                . "&cpu=$cpu&memory=$mem&hour=$hour&nb_devices=$nb_devices"
                . "&nb_services=$nb_services&nb_logs="
                . ($cache->get($k) || '0');
            AAT::Download::File($APPLI, $url, '/dev/null');
        }
    }
    AAT::Syslog::Message('octo_world_stats', 'WORLD_STATS_SENT');
}
else
{
    print "World Statistics are disabled !\n" if (!$quiet);
}

=head1 AUTHOR

Sebastien Thebert <octopussy@onetool.pm>

=head1 SEE ALSO

octo_dispatcher, octo_extractor, octo_parser, octo_uparser, octo_scheduler

=cut
